const std = @import("std");

const Source = @import("../../source.zig").Source;
const Semantic = @import("../../Semantic.zig");
const report = @import("../../reporter.zig");

const printer = @import("../../root.zig").printer;

const t = std.testing;
const print = std.debug.print;

var buf: [1024]u8 = undefined;

/// Build a Semantic from source, returning the raw Result so tests can
/// inspect errors. Unlike `build`, this does not fail on analysis errors -
/// callers are expected to assert on `result.hasErrors()` themselves.
pub fn buildWithErrors(src: [:0]const u8) !Semantic.Builder.Result {
    var builder = Semantic.Builder.init(t.allocator);
    errdefer builder.deinit();
    var source = try Source.fromString(
        t.allocator,
        try t.allocator.dupeZ(u8, src),
        try t.allocator.dupe(u8, "test.zig"),
    );
    defer source.deinit();
    builder.withSource(&source);
    const result = try builder.build(src);
    builder.deinit();
    return result;
}

pub fn build(src: [:0]const u8) !Semantic {
    const w = std.Io.File.stderr().writer(t.io, &buf);
    var stderr = w.interface;
    var r = try report.Reporter.graphical(
        t.io,
        &stderr,
        t.allocator,
        report.formatter.Graphical.Theme.unicodeNoColor(),
    );
    defer r.deinit();
    var builder = Semantic.Builder.init(t.allocator);
    var source = try Source.fromString(
        t.allocator,
        try t.allocator.dupeZ(u8, src),
        try t.allocator.dupe(u8, "test.zig"),
    );
    defer source.deinit();
    builder.withSource(&source);
    defer builder.deinit();

    var result = builder.build(src) catch |e| {
        print("Analysis failed on source:\n\n{s}\n\n", .{src});
        return e;
    };
    errdefer result.value.deinit();
    if (result.hasErrors()) {
        print("Analysis failed.\n", .{});
        r.reportErrors(result.errors.toManaged(t.allocator)) catch @panic("OOM");
        print("\nSource:\n\n{s}\n\n", .{src});
        return error.AnalysisFailed;
    }

    return result.value;
}

pub fn debugSemantic(semantic: *const Semantic) !void {
    var stderr_writer = std.Io.File.stderr().writer(t.io, &buf);
    var p = printer.Printer.init(t.allocator, &stderr_writer.interface);
    defer p.deinit();
    var sp = printer.SemanticPrinter.new(&p, semantic);

    print("Symbol table:\n\n", .{});
    try sp.printSymbolTable();

    print("\n\nUnresolved references:\n\n", .{});
    try sp.printUnresolvedReferences();

    print("\n\nScopes:\n\n", .{});
    try sp.printScopeTree();
    print("\n\n", .{});

    print("\n\nModules:\n\n", .{});
    try sp.printModuleRecord();
    print("\n\n", .{});
}
