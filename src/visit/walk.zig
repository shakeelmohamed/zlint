// Copyright (c) 2025 Don Isaac
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.
//! AST walking and visiting utilities.

pub const WalkState = enum {
    /// Keep walking as normal
    Continue,
    /// Keep walking, but do not walk this node's children.
    Skip,
    /// Stop the walk. `walker.walk()` will return.
    Stop,
};

/// Traverses an [AST](#zig.Ast) in a depth-first manner.
///
/// Visitors are structs (or some other container) that control the walk and
/// optionally inspect each node as it is encountered.
///
/// ## Visiting Nodes
/// For each kind of node (e.g. `fn_decl`), `Walker` will call `Visitor`'s
/// `visit_fn_decl` method with a mutable pointer to a `Visitor` instance and
/// the node's id. Visitors do not need methods for each kind of node; missing
/// visit methods will simply not be called.
///
/// For example:
/// ```zig
/// pub fn visit_fn_decl(this: *Visitor, node: Node.Index) Error!WalkState;
/// ```
///
/// > ### IMPORTANT
/// > Make sure you mark your visitor methods as `pub`!
///
/// Some nodes have a "full" representation in the AST, as defined by
/// [Ast.full].  Like tag visitors, full node visitors are called with a mutable
/// pointer to the visitor and the node id. However, they also receive a pointer
/// to the full node. T They are named `visitFullNodeName`. In general, if there
/// is a `full.NodeName` struct in [Ast.full], there will be a corresponding
/// `visitNodeName` method in the visitor.
///
/// For example:
/// ```zig
/// pub fn visitFnProto(this: *Visitor, node: Node.Index, fn_proto: *const Ast.full.FnProto) Error!WalkState;
/// ```
///
/// A couple of important notes and caveats:
/// - Full node visitors are called _instead_ of tag visitors. If you have a `visitFnProto` method,
///   `visit_fn_proto` will not be called.
/// - Zig's parser does not create a unique node for function parameters. However,
///   they are still visited with `visitFnParam`. This does not apply to`
///   arguments passed to function call expressions.
///
/// ## Controlling the Walk
///
/// Visit methods control the walk by returning a `WalkState`. This has the
/// following behavior:
/// - `Continue`: keep traversing the AST as normal
/// - `Skip`: do not stop traversal, but do not visit this node's children
/// - `Stop`: halt traversal. This is effectively `return`.
///
///
/// ## Hooks
/// Visitors may optionally have an `enterNode` and/or `exitNode` method. These
/// get called before and after visiting each node, respectively. Just like
/// visit methods, these are also passed a mutable pointer to a Visitor instance
/// and the current node's id.
pub fn Walker(Visitor: type, Error: type) type {
    const tag_info = @typeInfo(Node.Tag).@"enum";

    const VisitFn = fn (visitor: *Visitor, node: Node.Index) Error!WalkState;
    const VisitFull = struct {
        fn VisitFull(Full: type) type {
            return fn (visitor: *Visitor, node: Node.Index, var_decl: *const Full) Error!WalkState;
        }
    }.VisitFull;
    // const VisitFullVarDeclFn = fn (visitor: *Visitor, node: Node.Index, var_decl: Ast.full.VarDecl) Error!WalkState;
    // const VisitFullFnProto

    const WalkerVTable = struct {
        // SAFETY: set immediately when iterating over tag infos. Only one
        // vtable is ever instantiated.
        tag_table: [tag_info.fields.len]?*const VisitFn = undefined,

        // "full node" visitors
        visitVarDecl: ?*const VisitFull(full.VarDecl) = null,
        visitAssignDestructure: ?*const VisitFull(full.AssignDestructure) = null,
        visitIf: ?*const VisitFull(full.If) = null,
        visitWhile: ?*const VisitFull(full.While) = null,
        visitFor: ?*const VisitFull(full.For) = null,
        visitFnProto: ?*const VisitFull(full.FnProto) = null,
        visitFnParam: ?*const VisitFull(full.FnProto.Param) = null,
        visitContainerField: ?*const VisitFull(full.ContainerField) = null,
        visitStructInit: ?*const VisitFull(full.StructInit) = null,
        visitArrayInit: ?*const VisitFull(full.ArrayInit) = null,
        visitArrayType: ?*const VisitFull(full.ArrayType) = null,
        visitPtrType: ?*const VisitFull(full.PtrType) = null,
        visitSlice: ?*const VisitFull(full.Slice) = null,
        visitContainerDecl: ?*const VisitFull(full.ContainerDecl) = null,
        visitSwitchCase: ?*const VisitFull(full.SwitchCase) = null,
        visitAsm: ?*const VisitFull(full.Asm) = null,
        visitCall: ?*const VisitFull(full.Call) = null,
    };

    // stores pointers to Visitor's visit methods. Each kind of node (ie each
    // variant of Node.Tag) gets a similarly named visit function. For example,
    // when visiting a `fn_proto` node, a pointer to `visit_fn_proto` gets
    // stored in the vtable at the int index for `fn_proto`.
    //
    // The vtable is built inside a comptime block and stored as a `const` so
    // the returned type does not capture a reference to a comptime var (a
    // compile error since Zig 0.16).
    const vtable: WalkerVTable, const has_any_methods: bool = comptime blk: {
        var vt: WalkerVTable = .{};
        var has_methods = false;
        for (tag_info.fields) |field| {
            const id: usize = field.value;
            // e.g. visit_if_stmt, visit_less_than, visit_greater_than
            const visit_fn_name = "visit_" ++ field.name;

            // visitors who do not care about visiting certain kinds of nodes do not
            // need to implement visitor methods for them. Visiting will simply be
            // skipped, and walking will continue as normal.
            if (!@hasDecl(Visitor, visit_fn_name)) {
                vt.tag_table[id] = null;
                continue;
            }
            has_methods = true;
            if (@typeInfo(@TypeOf(@field(Visitor, visit_fn_name))) != .@"fn") {
                @compileError("Visitor method '" ++ @typeName(Visitor) ++ "." ++ visit_fn_name ++ "' must be function.");
            }
            vt.tag_table[id] = &@field(Visitor, visit_fn_name);
        }

        // "full node" visitors
        for (@typeInfo(WalkerVTable).@"struct".fields) |field| {
            const name = field.name;
            if (!std.mem.eql(u8, name, "tag_table")) {
                if (@hasDecl(Visitor, name)) {
                    @field(vt, name) = @field(Visitor, name);
                    has_methods = true;
                }
            }
        }
        break :blk .{ vt, has_methods };
    };

    // It would be confusing for _nothing_ to happen after a walk b/c you misspelled visit_container_field or something
    if (!has_any_methods and
        !@hasDecl(Visitor, "enterNode") and
        !@hasDecl(Visitor, "exitNode"))
    {
        @compileError("Visitor '" ++ @typeName(Visitor) ++ "' has no visit methods. Did you forget to make them `pub`?");
    }

    return struct {
        /// AST being walked.
        ast: *const Ast,
        // store these to avoid repeated pointer arithmetic. Borrowed from `ast`.
        // len isn't needed b/c we can get it from there, and lets us save 1 word
        // per ptr.

        visitor: *Visitor,
        stack: std.ArrayListUnmanaged(StackEntry) = .empty,
        alloc: Allocator,

        const Self = @This();
        const WalkError = Error || Allocator.Error;

        /// Initialize this walker's stack in preparation for a walk. It is
        /// recommended to use an arena for `allocator`.
        pub fn init(allocator: Allocator, ast: *const Ast, visitor: *Visitor) Allocator.Error!Self {
            var self = Self.new(allocator, ast, visitor);
            // var stackfb = std.heap.stackFallback(64, allocator);
            // const stack = stackfb.get();
            const root_decls = ast.rootDecls();
            try self.stack.ensureTotalCapacity(
                allocator,
                // 2x b/c each root decl gets an enter & exit node
                @max(ast.nodes.len >> 4, 8 + (2 * root_decls.len)),
            );

            for (0..root_decls.len) |i| {
                const node = root_decls[root_decls.len - i - 1];
                // entries are popped off end so exit nodes must be pushed first
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .exit });
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .enter });
            }

            return self;
        }

        pub const InitError = error{NullNode} || Allocator.Error;

        pub fn initAtNode(
            allocator: Allocator,
            ast: *const Ast,
            visitor: *Visitor,
            node: Node.Index,
        ) InitError!Self {
            if (node == .root) {
                @branchHint(.cold);
                return InitError.NullNode;
            }
            var self = Self.new(allocator, ast, visitor);
            try self.push(node, .{ .maybe_null = false });
            return self;
        }

        fn new(allocator: Allocator, ast: *const Ast, visitor: *Visitor) Self {
            return Self{
                .ast = ast,
                .alloc = allocator,
                .visitor = visitor,
            };
        }

        inline fn enterNode(self: *Self, node: Node.Index) Error!void {
            if (@hasDecl(Visitor, "enterNode")) return self.visitor.enterNode(node);
        }

        inline fn exitNode(self: *Self, node: Node.Index) void {
            if (@hasDecl(Visitor, "exitNode")) return self.visitor.exitNode(node);
        }

        /// Walk over a `zig.Ast` with the provided `Visitor`.
        pub fn walk(self: *Self) WalkError!void {
            // Used by some full AST node getters. The meaning of this node depends on each getter.
            // Since this is re-used, care must be taken to avoid storing a reference to (now-clobbered) data.
            //
            // The most at-risk case is visitors storing references to full nodes
            // after specialized visitor methods return.
            //
            // W.R.T walking:
            // - walk()'s main body only ever dispatches tag-visitor calls, which never use this buffer
            // - walkFull() enqueues full nodes into the stack before returning. This copies
            //   whatever's in this buffer, so it can't be clobbered.
            //
            var buf: [1]Node.Index = undefined;
            var buf2: [2]Node.Index = undefined;

            var i = if (comptime util.IS_DEBUG) @as(u32, 0);

            while (self.stack.pop()) |entry| {
                if (comptime util.IS_DEBUG) i += 1;
                if (entry.kind == .exit) {
                    print("[{}] exit: {s} ({})\n", .{ self.stack.items.len, @tagName(self.ast.nodeTag(entry.id)), @intFromEnum(entry.id) });
                    self.exitNode(entry.id);
                    continue;
                }
                const node = entry.id;
                const tag = self.ast.nodeTag(node);
                print("[{}] enter: {s} ({})\n", .{ self.stack.items.len, @tagName(tag), entry.id });
                print("{s}\n", .{self.ast.getNodeSource(entry.id)});

                try self.enterNode(node);

                // micro-optimization: avoid using`ast.fullFooNode` to 1) remove
                // redundant switch and 2) avoid passing &buffer when possible
                const should_stop: bool = try switch (tag) {
                    .global_var_decl,
                    .local_var_decl,
                    .simple_var_decl,
                    .aligned_var_decl,
                    => self.walkFull(full.VarDecl, node, tag, .visitVarDecl, .fullVarDecl, .{}),
                    // TODO: ensure `tag` is considered comptime-known

                    // ast.assignDestructure
                    .assign_destructure => self.walkFull(full.AssignDestructure, node, tag, .visitAssignDestructure, .assignDestructure, .{}),

                    .if_simple, .@"if" => self.walkFull(full.If, node, tag, .visitIf, .fullIf, .{}),
                    .while_simple, .while_cont, .@"while" => self.walkFull(full.While, node, tag, .visitWhile, .fullWhile, .{}),
                    .for_simple, .@"for" => self.walkFull(full.For, node, tag, .visitFor, .fullFor, .{}),

                    // ast.fullContainerField
                    .container_field_init => self.walkFull(full.ContainerField, node, tag, .visitContainerField, .containerFieldInit, .{}),
                    .container_field_align => self.walkFull(full.ContainerField, node, tag, .visitContainerField, .containerFieldAlign, .{}),
                    .container_field => self.walkFull(full.ContainerField, node, tag, .visitContainerField, .containerField, .{}),

                    // ast.fullFnProto
                    .fn_proto => self.walkFull(full.FnProto, node, tag, .visitFnProto, .fnProto, .{}),
                    .fn_proto_multi => self.walkFull(full.FnProto, node, tag, .visitFnProto, .fnProtoMulti, .{}),
                    .fn_proto_one => self.walkFull(full.FnProto, node, tag, .visitFnProto, .fnProtoOne, .{&buf}),
                    .fn_proto_simple => self.walkFull(full.FnProto, node, tag, .visitFnProto, .fnProtoSimple, .{&buf}),
                    // NOTE: .fn_decl is intentionally omitted. its lhs is `.fn_proto` and rhs is `.block*`,
                    // which get visited as expected via tag visitors + pushChildren

                    // ast.fullStructInit
                    .struct_init_one, .struct_init_one_comma => self.walkFull(full.StructInit, node, tag, .visitStructInit, .structInitOne, .{&buf}),
                    .struct_init_dot_two, .struct_init_dot_two_comma => self.walkFull(full.StructInit, node, tag, .visitStructInit, .structInitDotTwo, .{&buf2}),
                    .struct_init_dot, .struct_init_dot_comma => self.walkFull(full.StructInit, node, tag, .visitStructInit, .structInitDot, .{}),
                    .struct_init, .struct_init_comma => self.walkFull(full.StructInit, node, tag, .visitStructInit, .structInit, .{}),

                    // ast.fullArrayInit
                    .array_init_one, .array_init_one_comma => self.walkFull(full.ArrayInit, node, tag, .visitArrayInit, .arrayInitOne, .{&buf}),
                    .array_init_dot_two, .array_init_dot_two_comma => self.walkFull(full.ArrayInit, node, tag, .visitArrayInit, .arrayInitDotTwo, .{&buf2}),
                    .array_init_dot, .array_init_dot_comma => self.walkFull(full.ArrayInit, node, tag, .visitArrayInit, .arrayInitDot, .{}),
                    .array_init, .array_init_comma => self.walkFull(full.ArrayInit, node, tag, .visitArrayInit, .arrayInit, .{}),

                    // ast.fullArrayType
                    .array_type => self.walkFull(full.ArrayType, node, tag, .visitArrayType, .arrayType, .{}),
                    .array_type_sentinel => self.walkFull(full.ArrayType, node, tag, .visitArrayType, .arrayTypeSentinel, .{}),

                    // ast.fullPtrType
                    .ptr_type_aligned => self.walkFull(full.PtrType, node, tag, .visitPtrType, .ptrTypeAligned, .{}),
                    .ptr_type_sentinel => self.walkFull(full.PtrType, node, tag, .visitPtrType, .ptrTypeSentinel, .{}),
                    .ptr_type => self.walkFull(full.PtrType, node, tag, .visitPtrType, .ptrType, .{}),
                    .ptr_type_bit_range => self.walkFull(full.PtrType, node, tag, .visitPtrType, .ptrTypeBitRange, .{}),

                    // ast.fullSlice
                    .slice_open, .slice, .slice_sentinel => self.walkFull(full.Slice, node, tag, .visitSlice, .fullSlice, .{}),

                    // ast.fullContainerDecl
                    .root => unreachable,
                    .container_decl,
                    .container_decl_trailing,
                    .container_decl_arg,
                    .container_decl_arg_trailing,
                    .container_decl_two,
                    .container_decl_two_trailing,
                    .tagged_union,
                    .tagged_union_trailing,
                    .tagged_union_enum_tag,
                    .tagged_union_enum_tag_trailing,
                    .tagged_union_two,
                    .tagged_union_two_trailing,
                    => self.walkFull(full.ContainerDecl, node, tag, .visitContainerDecl, .fullContainerDecl, .{&buf2}),

                    // ast.fullSwitchCase
                    .switch_case_one, .switch_case_inline_one, .switch_case, .switch_case_inline => self.walkFull(full.SwitchCase, node, tag, .visitSwitchCase, .fullSwitchCase, .{}),

                    // ast.fullAsm
                    .asm_simple, .@"asm" => self.walkFull(full.Asm, node, tag, .visitAsm, .fullAsm, .{}),

                    // ast.fullCall
                    .call, .call_comma => self.walkFull(full.Call, node, tag, .visitCall, .callFull, .{}),
                    .call_one, .call_one_comma => self.walkFull(full.Call, node, tag, .visitCall, .callOne, .{&buf}),

                    else => tag_visit: {
                        const state: WalkState = if (vtable.tag_table[@intFromEnum(tag)]) |visit_fn|
                            try @call(.auto, visit_fn, .{ self.visitor, node })
                        else
                            WalkState.Continue;

                        switch (state) {
                            .Continue => {
                                try self.pushChildren(node, tag);
                                break :tag_visit false;
                            },
                            .Skip => break :tag_visit false,
                            .Stop => break :tag_visit true,
                        }
                    },
                };
                if (should_stop) return;
            }
        }

        /// Walk a "full" AST node, visiting it with one of the specialized visitors
        /// in the vtable. Employs uncomfortable amounts of comptime fuckery.
        ///
        /// Returns `true` when visitors stop the walk (`WalkState.Stop`).
        fn walkFull(
            self: *Self,
            /// Full AST struct definition from `Ast.full`. Assumed to have a
            /// `Components` member type.
            Full: type,
            node: Node.Index,
            tag: Node.Tag,
            /// name of specialized visitor fn in vtable (as a `.tag`)
            /// example: `.fnProto`
            comptime vtable_fn_name: anytype,
            /// Name of AST method (as a `.tag`) used to get the full node
            /// example: `.fullFnProto`
            comptime fullGetterName: anytype,
            /// Arguments to pass to `fullGetter`. AST pointer and node id will
            /// be prepended/appended (respectively). For example:
            /// `ast.fullFnProto(&buf, node)` -> `.{&buf}`
            args: anytype,
        ) WalkError!bool {
            // early checks for better error messages
            comptime {
                if (!@hasField(WalkerVTable, @tagName(vtable_fn_name)))
                    @compileError(@tagName(vtable_fn_name) ++ " is not a specialized visitor fn name.");

                if (!@hasDecl(Ast, @tagName(fullGetterName)))
                    @compileError("zig.Ast has no method named '" ++ @tagName(fullGetterName) ++ "'.");
            }

            const visit_full_fn: ?*const VisitFull(Full) = @field(vtable, @tagName(vtable_fn_name));

            // e.g.: const fn_proto = self.ast.fullFnProto(&buf, node)
            const full_args = .{self.ast.*} ++ args ++ .{node};
            const fullGetter = @field(Ast, @tagName(fullGetterName));
            // handle heterogenous return signatures
            const info = @typeInfo(@TypeOf(fullGetter));
            const full_node: Full = if (comptime @typeInfo(info.@"fn".return_type.?) == .optional)
                @call(.auto, fullGetter, full_args) orelse unreachable
            else
                @call(.auto, fullGetter, full_args);

            // Call the full visitor if one exists. Fall back to tag visitors.
            // Either way, the full node is still traversed correctly.
            const state: WalkState = if (visit_full_fn) |visitFullFn|
                try visitFullFn(self.visitor, node, &full_node)
            else if (vtable.tag_table[@intFromEnum(tag)]) |visitTagFn|
                try visitTagFn(self.visitor, node)
            else
                WalkState.Continue;

            switch (state) {
                .Continue => try self.pushFullNode(Full.Components, node, full_node.ast),
                .Skip => return false,
                .Stop => return true,
            }

            // fn params don't have their own node. We need to hack in a visit()
            // for them. Even though params is typed as []const Node.Index, it
            // really isn't. Attempts to visit them as-is often leads to infinite
            // recursion.
            if (comptime Full == full.FnProto) {
                const Item = Node.Index;
                var temp = std.heap.stackFallback(8 * @sizeOf(Item), self.alloc);
                var parambuf = std.array_list.Managed(Item).init(temp.get());
                defer parambuf.deinit();
                try parambuf.ensureTotalCapacityPrecise(8);

                var it: full.FnProto.Iterator = full_node.iterate(self.ast);
                while (it.next()) |param| {
                    if (comptime vtable.visitFnParam) |visitFnParam| {
                        switch (try visitFnParam(self.visitor, node, param)) {
                            .Continue => {},
                            .Skip => continue,
                            .Stop => return true,
                        }
                    }
                    if (param.type_expr) |type_node| {
                        if (type_node != .root) try parambuf.append(type_node);
                    }
                }
                std.mem.reverse(Item, parambuf.items);
                try self.pushManyUnconditional(parambuf.items);
            }
            return false;
        }

        /// Use comptime fuckery to push a `FullNode.Components` subtree onto
        /// the stack in reverse order.
        fn pushFullNode(self: *Self, Components: type, own_id: Node.Index, components: Components) Allocator.Error!void {
            const info = @typeInfo(Components);
            const fields = info.@"struct".fields;
            // @compileLog(fields);
            try self.stack.ensureUnusedCapacity(self.alloc, 2 * fields.len);

            // NOTE: std.mem.reverseIterator does not work in comptime
            inline for (0..fields.len) |i| {
                const idx = fields.len - i - 1;
                const field = fields[idx];
                const is_token = comptime mem.endsWith(u8, field.name, "_token") or
                    token_names.has(field.name);

                const is_specific_undesirable_node = comptime Components == full.FnProto.Components and
                    (std.mem.eql(u8, field.name, "proto_node") or
                        std.mem.eql(u8, field.name, "params"));

                if (!is_token and !is_specific_undesirable_node) {
                    const subnode: field.type = @field(components, field.name);
                    switch (@TypeOf(subnode)) {
                        []const Node.Index, []Node.Index => {
                            try self.ensureStackCapacity(subnode.len);
                            for (0..subnode.len) |j| {
                                const el: Node.Index = subnode[subnode.len - j - 1];
                                util.assert(el != own_id, "Cycle detected on node {d}", .{@intFromEnum(own_id)});
                                self.push(el, .{ .assume_capacity = true });
                            }
                        },
                        Node.Index => if (subnode != .root) {
                            if (comptime std.mem.eql(u8, field.name, "proto_node")) {
                                if (own_id != subnode) try self.push(subnode, .{ .maybe_null = false });
                            } else {
                                util.assert(subnode != own_id, "Cycle detected on node {d}", .{@intFromEnum(own_id)});
                                try self.push(subnode, .{ .maybe_null = false });
                            }
                        },
                        Node.OptionalIndex => {
                            if (subnode.unwrap()) |n| {
                                if (n != .root) {
                                    util.assert(n != own_id, "Cycle detected on node {d}", .{@intFromEnum(own_id)});
                                    try self.push(n, .{ .maybe_null = false });
                                }
                            }
                        },
                        ?Node.Index => {
                            if (subnode) |n| {
                                if (n != .root) {
                                    util.assert(n != own_id, "Cycle detected on node {d}", .{@intFromEnum(own_id)});
                                    try self.push(n, .{ .maybe_null = false });
                                }
                            }
                        },
                        bool, ?Ast.TokenIndex => {},
                        else => |Unsupported| {
                            @compileError(@typeName(Components) ++ "." ++ field.name ++ " has unsupported type " ++ @typeName(Unsupported) ++ ".");
                        },
                    }
                }
            }
        }

        const token_names = std.StaticStringMap(void).initComptime(.{
            .{"lparen"},
            .{"rparen"},
            .{"lbrace"},
            .{"rbrace"},
            .{"lbracket"},
            .{"rbracket"},
        });

        fn pushChildren(self: *Self, node: Node.Index, tag: Node.Tag) Allocator.Error!void {
            const DataVariant = NodeDataVariant.from(tag);
            switch (DataVariant) {
                .node => try self.push(self.ast.nodeData(node).node, .{}),
                .opt_node => {
                    if (self.ast.nodeData(node).opt_node.unwrap()) |n| try self.push(n, .{ .maybe_null = false });
                },
                .node_and_node => {
                    const pair = self.ast.nodeData(node).node_and_node;
                    try self.ensureStackCapacity(2);
                    self.push(pair[1], .{ .assume_capacity = true, .maybe_null = false });
                    self.push(pair[0], .{ .assume_capacity = true, .maybe_null = false });
                },
                .opt_node_and_opt_node => {
                    const pair = self.ast.nodeData(node).opt_node_and_opt_node;
                    try self.ensureStackCapacity(2);
                    if (pair[1].unwrap()) |n| self.push(n, .{ .assume_capacity = true, .maybe_null = false });
                    if (pair[0].unwrap()) |n| self.push(n, .{ .assume_capacity = true, .maybe_null = false });
                },
                .node_and_opt_node => {
                    const pair = self.ast.nodeData(node).node_and_opt_node;
                    try self.ensureStackCapacity(2);
                    if (pair[1].unwrap()) |n| self.push(n, .{ .assume_capacity = true, .maybe_null = false });
                    self.push(pair[0], .{ .assume_capacity = true, .maybe_null = false });
                },
                .opt_node_and_node => {
                    const pair = self.ast.nodeData(node).opt_node_and_node;
                    try self.ensureStackCapacity(2);
                    self.push(pair[1], .{ .assume_capacity = true, .maybe_null = false });
                    if (pair[0].unwrap()) |n| self.push(n, .{ .assume_capacity = true, .maybe_null = false });
                },
                .node_and_token => try self.push(self.ast.nodeData(node).node_and_token[0], .{}),
                .token_and_node => try self.push(self.ast.nodeData(node).token_and_node[1], .{}),
                .opt_token_and_node => try self.push(self.ast.nodeData(node).opt_token_and_node[1], .{}),
                .opt_node_and_token => {
                    if (self.ast.nodeData(node).opt_node_and_token[0].unwrap()) |n| try self.push(n, .{ .maybe_null = false });
                },
                .opt_token_and_opt_node => {
                    if (self.ast.nodeData(node).opt_token_and_opt_node[1].unwrap()) |n| try self.push(n, .{ .maybe_null = false });
                },
                .node_and_extra => {
                    const pair = self.ast.nodeData(node).node_and_extra;
                    const range = self.ast.extraData(pair[1], Node.SubRange);
                    const members = self.ast.extraDataSlice(range, Node.Index);
                    try self.pushManyUnconditional(members);
                    try self.push(pair[0], .{});
                },
                .extra_and_node => try self.push(self.ast.nodeData(node).extra_and_node[1], .{}),
                .extra_and_opt_node => {
                    if (self.ast.nodeData(node).extra_and_opt_node[1].unwrap()) |n| try self.push(n, .{ .maybe_null = false });
                },
                .extra_range => {
                    const range = self.ast.nodeData(node).extra_range;
                    const members = self.ast.extraDataSlice(range, Node.Index);
                    try self.pushManyUnconditional(members);
                },
                .none => {},
            }
        }

        const NodeDataVariant = enum {
            node,
            opt_node,
            node_and_node,
            opt_node_and_opt_node,
            node_and_opt_node,
            opt_node_and_node,
            node_and_token,
            token_and_node,
            opt_token_and_node,
            opt_node_and_token,
            opt_token_and_opt_node,
            node_and_extra,
            extra_and_node,
            extra_and_opt_node,
            extra_range,
            none,

            fn from(tag: Node.Tag) NodeDataVariant {
                return switch (tag) {
                    // .node
                    .deref,
                    .@"defer",
                    .@"suspend",
                    .@"resume",
                    .@"comptime",
                    .@"nosuspend",
                    .@"try",
                    .bool_not,
                    .negation,
                    .bit_not,
                    .negation_wrap,
                    .address_of,
                    .optional_type,
                    => .node,

                    // .opt_node
                    .@"return" => .opt_node,

                    // .node_and_node
                    .@"catch",
                    .equal_equal,
                    .bang_equal,
                    .less_than,
                    .greater_than,
                    .less_or_equal,
                    .greater_or_equal,
                    .assign_mul,
                    .assign_div,
                    .assign_mod,
                    .assign_add,
                    .assign_sub,
                    .assign_shl,
                    .assign_shl_sat,
                    .assign_shr,
                    .assign_bit_and,
                    .assign_bit_xor,
                    .assign_bit_or,
                    .assign_mul_wrap,
                    .assign_add_wrap,
                    .assign_sub_wrap,
                    .assign_mul_sat,
                    .assign_add_sat,
                    .assign_sub_sat,
                    .assign,
                    .merge_error_sets,
                    .mul,
                    .div,
                    .mod,
                    .array_mult,
                    .mul_wrap,
                    .mul_sat,
                    .add,
                    .sub,
                    .array_cat,
                    .add_wrap,
                    .sub_wrap,
                    .add_sat,
                    .sub_sat,
                    .shl,
                    .shl_sat,
                    .shr,
                    .bit_and,
                    .bit_xor,
                    .bit_or,
                    .@"orelse",
                    .bool_and,
                    .bool_or,
                    .array_type,
                    .slice_open,
                    .array_access,
                    .array_init_one,
                    .array_init_one_comma,
                    .switch_range,
                    .error_union,
                    .while_simple,
                    .for_simple,
                    .if_simple,
                    .fn_decl,
                    .container_field_align,
                    => .node_and_node,

                    // .opt_node_and_opt_node
                    .simple_var_decl,
                    .array_init_dot_two,
                    .array_init_dot_two_comma,
                    .struct_init_dot_two,
                    .struct_init_dot_two_comma,
                    .fn_proto_simple,
                    .builtin_call_two,
                    .builtin_call_two_comma,
                    .container_decl_two,
                    .container_decl_two_trailing,
                    .tagged_union_two,
                    .tagged_union_two_trailing,
                    .block_two,
                    .block_two_semicolon,
                    => .opt_node_and_opt_node,

                    // .node_and_opt_node
                    .aligned_var_decl,
                    .container_field_init,
                    .struct_init_one,
                    .struct_init_one_comma,
                    .call_one,
                    .call_one_comma,
                    .for_range,
                    => .node_and_opt_node,

                    // .opt_node_and_node
                    .ptr_type_aligned,
                    .ptr_type_sentinel,
                    .switch_case_one,
                    .switch_case_inline_one,
                    => .opt_node_and_node,

                    // .node_and_extra
                    .array_type_sentinel,
                    .slice,
                    .slice_sentinel,
                    .array_init,
                    .array_init_comma,
                    .struct_init,
                    .struct_init_comma,
                    .call,
                    .call_comma,
                    .container_decl_arg,
                    .container_decl_arg_trailing,
                    .tagged_union_enum_tag,
                    .tagged_union_enum_tag_trailing,
                    .container_field,
                    .@"asm",
                    .@"if",
                    .@"switch",
                    .switch_comma,
                    .while_cont,
                    .@"while",
                    => .node_and_extra,

                    // .extra_and_node
                    .assign_destructure,
                    .switch_case,
                    .switch_case_inline,
                    .ptr_type,
                    .ptr_type_bit_range,
                    => .extra_and_node,

                    // .extra_and_opt_node
                    .global_var_decl,
                    .local_var_decl,
                    .fn_proto_multi,
                    .fn_proto_one,
                    .fn_proto,
                    => .extra_and_opt_node,

                    // .node_and_token
                    .field_access,
                    .unwrap_optional,
                    .grouped_expression,
                    .asm_input,
                    .asm_simple,
                    => .node_and_token,

                    // .token_and_node
                    .anyframe_type => .token_and_node,

                    // .opt_token_and_node
                    .test_decl, .@"errdefer" => .opt_token_and_node,

                    // .opt_node_and_token
                    .asm_output => .opt_node_and_token,

                    // .opt_token_and_opt_node
                    .@"continue", .@"break" => .opt_token_and_opt_node,

                    // .extra_range
                    .root,
                    .array_init_dot,
                    .array_init_dot_comma,
                    .struct_init_dot,
                    .struct_init_dot_comma,
                    .builtin_call,
                    .builtin_call_comma,
                    .container_decl,
                    .container_decl_trailing,
                    .tagged_union,
                    .tagged_union_trailing,
                    .block,
                    .block_semicolon,
                    => .extra_range,

                    // .none - no child nodes
                    .multiline_string_literal,
                    .error_set_decl,
                    .anyframe_literal,
                    .char_literal,
                    .number_literal,
                    .unreachable_literal,
                    .identifier,
                    .enum_literal,
                    .string_literal,
                    .error_value,
                    => .none,

                    // .@"for" is handled separately in walk()
                    .@"for" => .none,
                };
            }
        };

        const PushOpts = struct { maybe_null: bool = true, assume_capacity: bool = false };
        /// Push a child node onto the stack.
        /// - `maybe_null`: if true, the node will not be pushed if it is null. When false,
        ///   a runtime safety check is performed to ensure the node is not null.
        /// - `assume_capacity`: if true, the stack's capacity is assumed to be sufficient
        ///   for the new node, and this method will use `appendAssumeCapacity`. Useful for
        ///   pushing many nodes at once.
        inline fn push(self: *Self, node: Node.Index, comptime opts: PushOpts) if (opts.assume_capacity) void else Allocator.Error!void {
            if (comptime opts.maybe_null) {
                if (node == .root) return;
            }
            if (comptime util.RUNTIME_SAFETY) self.assertInRange(node);
            if (comptime opts.assume_capacity) {
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .exit });
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .enter });
            } else {
                try self.stack.append(self.alloc, .{ .id = node, .kind = .exit });
                return self.stack.append(self.alloc, .{ .id = node, .kind = .enter });
            }
        }

        /// Push many nodes, assuming none of them are null
        inline fn pushManyUnconditional(self: *Self, nodes: []const Node.Index) Allocator.Error!void {
            try self.ensureStackCapacity(nodes.len);
            var it = std.mem.reverseIterator(nodes);
            while (it.next()) |node| {
                self.assertInRange(node);
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .exit });
                self.stack.appendAssumeCapacity(.{ .id = node, .kind = .enter });
            }
        }

        /// Ensure unused capacity for `node_count` new nodes.
        inline fn ensureStackCapacity(self: *Self, node_count: usize) Allocator.Error!void {
            try self.stack.ensureUnusedCapacity(self.alloc, 2 * node_count);
        }

        /// Sanity check that is only enabled in safe/debug builds. Inlined so
        /// compiler can optimize away checks that aren't needed.
        inline fn assertInRange(self: *Self, node: Node.Index) void {
            util.assert(node != .root, "Tried to push null node onto stack", .{});

            if (comptime util.RUNTIME_SAFETY) {
                if (@intFromEnum(node) >= self.ast.nodes.len) {
                    print(
                        "Tried to push out-of-bounds node (id: {d}, nodes.len: {d}). It's likely a token or extra_data index.",
                        .{ @intFromEnum(node), self.ast.nodes.len },
                    );
                    self.printStack();
                    @panic("node index out of bounds");
                }
            }

            if (comptime util.IS_DEBUG) self.checkForVisitLoop(node);
        }

        fn checkForVisitLoop(self: *const Self, node: Node.Index) void {
            @branchHint(.cold);
            for (self.stack.items) |seen| {
                if (seen.kind == .exit) continue;
                if (seen.id == node) {
                    print("\nFound a loop while walking an AST: Node {d} is already being visited.\n", .{node});
                    print("Stack:\n", .{});
                    self.printStack();
                    @panic("Tried to visit the same node twice.");
                }
            }
        }

        fn printStack(self: *const Self) void {
            print("Stack:\n", .{});
            for (self.stack.items) |el| {
                if (el.kind == .exit) continue;
                print("- {d}: {s}\n", .{ @intFromEnum(el.id), @tagName(self.ast.nodeTag(el.id)) });
            }
        }

        pub fn deinit(self: *Self) void {
            self.stack.deinit(self.alloc);
        }
    };
}

const StackEntry = struct {
    id: Node.Index,
    kind: Kind = .enter,
    pub const Kind = enum { enter, exit };
};

// toggle debug printing in walk(). Dont commit PRs with this still set to `true`.
const DEBUG_PRINT = false;
inline fn print(comptime fmt: []const u8, args: anytype) void {
    comptime if (!DEBUG_PRINT) return;
    std.debug.print(fmt ++ "\n", args);
}
comptime {
    if (@import("builtin").mode != .Debug) assert(DEBUG_PRINT == false);
}

const std = @import("std");
const util = @import("util");
const mem = std.mem;
const assert = std.debug.assert;
const Allocator = mem.Allocator;

const Semantic = @import("../Semantic.zig");
const Ast = Semantic.Ast;
const full = Ast.full;
const Node = Ast.Node;

// =============================================================================
const expectEqual = std.testing.expectEqual;
const expect = std.testing.expect;

test {
    _ = @import("walk_test.zig");
}

test Walker {
    // This visitor has methods that get called during the walk.
    const Foo = struct {
        depth: u32 = 0,
        nodes_visited: u32 = 0,
        seen_fn_decl: bool = false,
        seen_var_decl: bool = false,

        const Error = error{};

        fn enterNode(self: *@This(), _: Node.Index) !void {
            self.depth += 1;
            self.nodes_visited += 1;
        }
        fn exitNode(self: *@This(), _: Node.Index) void {
            expect(self.depth > 0) catch @panic("expect failed");
            self.depth -= 1;
        }

        // All nodes may be visited with a function called `visit_[tag]`,
        // where `tag` is a variant of `Node.Tag`.
        fn visit_fn_decl(self: *@This(), _: Node.Index) Error!WalkState {
            self.seen_fn_decl = true;
            return .Continue;
        }

        /// You may also visit "full" nodes. Basically, if `Ast` has a method
        /// called `fullNodeKind`, then you can visit it with `visitNodeKind`.
        ///
        /// See `zig.Ast.full` for a list.
        fn visitVarDecl(
            self: *@This(),
            _: Node.Index,
            _: *const Ast.full.VarDecl,
        ) Error!WalkState {
            self.seen_var_decl = true;
            return .Continue;
        }
    };

    const allocator = std.testing.allocator;
    const src: [:0]const u8 =
        \\const std = @import("std");
        \\fn foo() void {
        \\  const x = 1;
        \\  std.debug.print("{d}\n", .{x});
        \\}
    ;

    // parse some source code into a zig.Ast
    var ast = try Ast.parse(allocator, src, .zig);
    defer ast.deinit(allocator);
    try expectEqual(0, ast.errors.len);

    // walk the AST with your visitor
    var visitor = Foo{};
    var walker = try Walker(Foo, Foo.Error).init(allocator, &ast, &visitor);
    defer walker.deinit();
    try walker.walk();

    try expectEqual(0, visitor.depth);
    try expectEqual(16, visitor.nodes_visited);
    try expect(visitor.seen_fn_decl);
    try expect(visitor.seen_var_decl);
}
